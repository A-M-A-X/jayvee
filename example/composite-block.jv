// SPDX-FileCopyrightText: 2023 Friedrich-Alexander-Universitat Erlangen-Nurnberg
//
// SPDX-License-Identifier: AGPL-3.0-only

composite blocktype CSVExtractor {
    property url oftype text;

    input inputName oftype None;
    output outputName oftype Sheet;

    block FileExtractor oftype HttpExtractor { url: url; }

	block FileTextInterpreter oftype TextFileInterpreter {}

	// Todo should be property
	block FileCSVInterpreter oftype CSVInterpreter { enclosing: '"'; }

    inputName
        ->FileExtractor
        ->FileTextInterpreter
        ->FileCSVInterpreter
        ->outputName;
}

pipeline CarsPipeline {
	CarsCSVExtractor 
	   	-> CarsTableInterpreter
		-> CarsLoader;

    block CarsCSVExtractor oftype CSVExtractor {
        url: "https://gist.githubusercontent.com/noamross/e5d3e859aa0c794be10b/raw/b999fb4425b54c63cab088c0ce2c0d6ce961a563/cars.csv";
    }

	block CarsTableInterpreter oftype TableInterpreter {
		header: true;
		columns: [
			"name" oftype text,
			"mpg" oftype decimal,
			"cyl" oftype integer,
			"disp" oftype decimal,
			"hp" oftype integer,
			"drat" oftype decimal,
			"wt" oftype decimal,
			"qsec" oftype decimal,
			"vs" oftype integer,
			"am" oftype integer,
			"gear" oftype integer,
			"carb" oftype integer
		];
	}

	block CarsLoader oftype SQLiteLoader {
		table: "Cars";
		file: "./compositeblockstest.sqlite";
	}
}