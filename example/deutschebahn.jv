valuetype IFOPT oftype text {
    constraints: [ IFOPT_Format ];
}

constraint IFOPT_Format oftype RegexConstraint {
    regex: /[a-z]{2}:\d+:\d+(:\d+)?/;
}

valuetype DB_Traffic oftype text {
    constraints: [ TrafficValueRange ];
}

constraint TrafficValueRange oftype WhitelistConstraint {
    whitelist: [ "FV", "RV", "nur DPN" ];
}

valuetype Longitude oftype decimal {
    constraints: [ GeographicCoordinateRange ];
}

valuetype Latitude oftype decimal {
    constraints: [ GeographicCoordinateRange ];
}

constraint GeographicCoordinateRange oftype RangeConstraint {
    lowerBound: -90;
    lowerBoundInclusive: true;
    upperBound: +90;
    upperBoundInclusive: true;
}

pipeline DbStopsPipeline {

    block DbStopsExtracor oftype HttpExtractor {
        url: "https://download-data.deutschebahn.com/static/datasets/haltestellen/D_Bahnhof_2020_alle.CSV";
    }

    DbStopsExtracor -> DbStopsCSVInterpreter;

    block DbStopsCSVInterpreter oftype CSVInterpreter {
        delimiter: ";";
    }

    DbStopsCSVInterpreter -> DbStopsTableInterpreter;

    block DbStopsTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "EVA_NR" oftype integer,
            "DS100" oftype text,
            "IFOPT" oftype IFOPT,
            "NAME" oftype text,
            "Verkehr" oftype DB_Traffic,
            "Laenge" oftype Longitude,
            "Breite" oftype Latitude,
            "Betreiber_Name" oftype text,
            "Betreiber_Nr" oftype integer,
            "Status" oftype text
        ];
    }

    DbStopsTableInterpreter -> DbStopsSQLiteLoader;

    block DbStopsSQLiteLoader oftype SQLiteLoader {
        table: "DB_stops";
        file: "./deutschebahn.sqlite";
    }
}